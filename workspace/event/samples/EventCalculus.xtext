grammar uk.ac.open.event.EventCalculus with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate eventcalculus "http://www.ac.uk/open/EventCalculus"

Model:
	(imports+=Import)*
	(declarations+=Declaration | statements+=Statement)*;

Import:
	'load'
	importURI=STRING;

Annotation: ';' '#' type=ANNOTATION_TYPE (value=STRING | value='not')?;

enum ANNOTATION_TYPE: G='generated' | A='author';

Declaration:
	DefSort | DefRange | DefOption | DefCompletion | DefNonInertia | DefXor | DefMutex;

Defines: DefSort | Definition;

DefSort:
	(annotations+=Annotation)*
	(reified='reified')? 'sort' name=ID (':' type=ID)?;

DefRange:
	(annotations+=Annotation)*
	'range' name=[DefSort] min=INT max=INT;

DefOption:
	(annotations+=Annotation)*
  	(  'option' name=ID value=INT
	| 'option' name=ID value2=ID);

DefCompletion:
	(annotations+=Annotation)*
	'completion' type=LabelType? label=[Definition];

DefNonInertia:
	(annotations+=Annotation)*
	'noninertial' names+=[Definition] (',' names+=[Definition])*;

DefXor:
	(annotations+=Annotation)*
	'xor' names+=[Definition] (',' names+=[Definition])*;

DefMutex:
	(annotations+=Annotation)*
	'mutex' names+=[Definition] (',' names+=[Definition])*;

Statement: SortDefinition | LabeledExpression;

LabeledExpression:
	(annotations+=Annotation)*
	(name=LabelType ':')? 
	expr=Expression '.';

enum LabelType: T='Theta' | D='Delta';

SortDefinition:
	sort=[DefSort] definitions+=Definition (',' definitions+=Definition)*;
	
Definition:
	name=ID ('(' (args+=[Defines])? (',' args+=[Defines])* ')')? (':' return+=[Defines])?;
	
Expression:
	expr=AssignmentOp | 
	qualifiers=Qualifier expr=Expression;

Qualifier: Exists | Forall;

Parameters: names+=[Defines] ("," names+=[Defines])*;

Exists returns Qualifier: (op=UnaryOp)? '{' parameters=Parameters '}';

Forall returns Qualifier: (op=UnaryOp)? '[' parameters=Parameters ']';

AssignmentOp returns Expression:
	OrExp (({AssignPlus.left=current} '->' | {AssignMin.left=current} '<->') 
		q=Qualifier? right=OrExp)?;

OrExp returns Expression:
	AndExp ({Or.left=current} '|' q=Qualifier? right=AndExp)*;

AndExp returns Expression:
	Relational ({And.left=current} '&' q=Qualifier? right=Relational)*;

Relational returns Expression:
	Addition (({RelNotEq.left=current} '!=' | {RelEqEq.left=current} '==' | {RelLtEq.left=current} '>=' |
	{RelGtEq.left=current} '<=' |
	{RelEq.left=current} '=' | {RelLt.left=current} '<' | {RelGt.left=current} '>') right=Addition)?;

Addition returns Expression:
	Multiplication (({Plus.left=current} '+' | {Minus.left=current} '-') right=Multiplication)*;

Multiplication returns Expression:
	Power (({Multi.left=current} '*' | {Div.left=current} '/') right=Power)*;

Power returns Expression:
	UnaryExpr (({Pow.left=current} '^') right=UnaryExpr)*;

UnaryExpr returns Expression:
	(op=UnaryOp)? right=PrePrimaryExpr;

enum UnaryOp:
	NOT='!';

PrePrimaryExpr returns Expression:
	TerminalExpression | AbstractRef | '(' Expression ')' | '-' n=INT;

AbstractRef returns Expression:
	define=[Defines]
		(
			({FunctionRef.func=current} ('(' (args+=Expression)? (',' args+=Expression)*')') ) 
		)?;

TerminalExpression:
	{IntLiteral} value=INT 
	| {StringLiteral} value=STRING |
	{BooleanLiteral} value=BOOLEAN;

	// -----
FQN:
	ID ('.' ID)*;

terminal INT returns ecore::EInt:
	'0' | '1'..'9' ('0'..'9')*;

terminal BOOLEAN returns ecore::EBoolean:
	'true' | 'false';

terminal ID:
	('a'..'z' | 'A'..'Z' | '_') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9')*;

terminal STRING:
	'"' ('\\' ('b' | 't' | 'n' | 'f' | 'r' | 'u' | '"' | '\\') | !('\\' | '"'))* '"' | "'" ('\\' ('b' | 't' | 'n' | 'f' |
	'r' | 'u' | "'" | '\\') | !('\\' | "'"))* "'";

terminal ML_COMMENT:
	'/*'->'*/';

terminal SL_COMMENT: ';' !('\n'|'\r'|'#')* ('\r'? '\n')?;
terminal WS: (' ' | '\t' | '\r' | '\n')+;
terminal ANY_OTHER:
	.;