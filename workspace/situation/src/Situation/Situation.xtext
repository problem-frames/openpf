grammar eu.securechange.Situation with org.eclipse.xtext.common.Terminals
generate situation "http://securechange.eu/situation" 

Situation: ("model" time=ID ":")? (things+=Thing)*;
Thing: Entity | Relationship | Domain;
Entity: type=EntityType name=ID;
Relationship: type=RelationshipType "(" source=[Thing] "," target=[Thing] ")";
Domain: name=ID type=DomainType properties+=Entity ("," properties+=Entity)*;
enum EntityType: 
    DomainAssumption="DA" | Function="FR" | Security="SR" | Anti="AR" | QualityProposition="Q" | Argument="A"
  | Actor="actor" | Action="action" | Attacker="attacker" | Process="process" | Activity="activity" | Resource="resource" | Asset="asset" 
  | Event="event";
enum DomainType: Specification="S" | Requirement="R" | World="W";

enum RelationshipType: 
     Wants="wants" 
    |Does="does" 
    |AND="and" | OR="or"
    |TRUST="trusts" | DELEGATE="delegates"
    |HURT="-" | HELP="+" | BREAK="--" | MAKE="++"
	|Fulfils="fulfils" 
	|Provides="provides"|Consumes="consumes"
	|Exploits="exploits"|Damages="damages"|Attacks="attacks"
	|Argues="argues";
terminal ID: ('#' (!('#'))+ '#') | ('^'? ('a'..'z' | 'A'..'Z' | '_' | '.') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | '.')*);